apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0-beta"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    api "io.reactivex.rxjava2:rxjava:2.1.14-RC1"
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        myPublication(MavenPublication) {
            groupId 'com.softwarehut'
            artifactId 'SHRepository'
            version '1.0.0-beta'
            artifact './build/outputs/aar/repository-release.aar'
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the api dependencies, adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

    // Repositories *to* which Gradle can publish artifacts
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}